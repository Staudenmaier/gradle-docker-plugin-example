import de.gesellix.gradle.docker.tasks.*

import static groovy.json.JsonOutput.prettyPrint
import static groovy.json.JsonOutput.toJson

buildscript {
  repositories {
    mavenLocal()
    maven { url 'http://dl.bintray.com/gesellix/docker-utils' }
    mavenCentral()
  }

  dependencies {
    classpath 'de.gesellix:gradle-docker-plugin:2014-07-07T00-32-53'
  }
}

apply plugin: 'de.gesellix.docker'
apply plugin: 'groovy'

repositories {
  mavenLocal()
  maven { url 'http://dl.bintray.com/gesellix/docker-utils' }
  mavenCentral()
}

docker {
  dockerHost = "http://172.17.42.1:4243/"
  authConfigPlain = ["username"     : "gesellix",
                     "password"     : "-yet-another-password-",
                     "email"        : "tobias@gesellix.de",
                     "serveraddress": "https://index.docker.io/v1/"]
}

task buildImage(type: DockerBuildTask) {
  imageName = "gesellix/example"
  buildContextDirectory = file('./docker/')
}

task pushImage(type: DockerPushTask) {
  dependsOn buildImage

  repositoryName = "gesellix/example"
  registry = "localhost:5000"
}

task pullImageOnRemoteServer(type: DockerPullTask) {
  dependsOn pushImage

  dockerHost = "http://172.17.42.1:4243/"
  imageName = "gesellix/example"
  registry = "localhost:5000"
}

task stopContainerOnRemoteServer(type: DockerStopTask) {
  dependsOn pullImageOnRemoteServer

  dockerHost = "http://172.17.42.1:4243/"
  containerId = "a_unique_name"
}

task rmOldContainerOnRemoteServer(type: DockerRmTask) {
  dependsOn stopContainerOnRemoteServer

  dockerHost = "http://172.17.42.1:4243/"
  containerId = "a_unique_name"
}

task runContainerOnRemoteServer(type: DockerRunTask) {
  dependsOn rmOldContainerOnRemoteServer

  dockerHost = "http://172.17.42.1:4243/"
  imageName = "localhost:5000/gesellix/example"
  containerName = "a_unique_name"
  containerConfiguration = [
      "ExposedPorts": ["8889/tcp": [:], "9300/tcp": [:]]]
  hostConfiguration = [
      "PortBindings": ["8889/tcp": [["HostPort": "8889"]]]]
}

task listContainersOnRemoteServer(type: DockerPsTask) {
  dockerHost = "http://172.17.42.1:4243/"

  doLast {
    println prettyPrint(toJson(containers))
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.0'
  distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
