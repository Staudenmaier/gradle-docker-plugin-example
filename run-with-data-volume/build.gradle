import de.gesellix.gradle.docker.tasks.*

ext {
  volumeDir = "/tmp"
}

task createDataContainer(type: DockerCreateTask) {
  imageName = "gesellix/docker-client-testimage"
  containerName = "data-volume"
  containerConfiguration = [
      "Cmd"       : ["-"],
      "Image"     : "gesellix/run-with-data-volumes",
      "HostConfig": [
          "Binds": ["$volumeDir:/data"]
      ]
  ]
}

task runContainerWithDataVolume(type: DockerRunTask) {
  dependsOn createDataContainer
  imageName = "gesellix/docker-client-testimage"
  containerName = "service-example"
  containerConfiguration = [
      "Cmd"       : ["true"],
      "HostConfig": [
          "VolumesFrom": ["data-volume"]
      ]
  ]
}

task inspectServiceContainer(type: DockerInspectContainerTask) {
  dependsOn runContainerWithDataVolume
  containerId = "service-example"

  doLast {
    logger.info("${containerInfo.content}")
  }
}

task stopServiceContainer(type: DockerStopTask) {
  dependsOn inspectServiceContainer
  containerId = "service-example"
}

task rmServiceContainer(type: DockerRmTask) {
  dependsOn stopServiceContainer
  containerId = "service-example"
}

task rmDataVolumeContainer(type: DockerRmTask) {
  dependsOn rmServiceContainer
  containerId = "data-volume"
}

runContainerWithDataVolume.finalizedBy rmDataVolumeContainer
